package Graphik.Analisis;
import Ast.*;
import Interfaz.Inicio;

import java_cup.runtime.*;

parser code
{:
    /**Metodo al que se llama automáticamente ante algún error sintactico.*/
    public void syntax_error(Symbol s){
       /* System.err.println("Error en la Línea " + (s.right+1) +" Columna "+(s.left+1)+ ". Identificador "
        +s.value + " no reconocido." );*/
        Inicio.reporteError.agregar("Sintactico",s.right+1,s.left+1,"Identificador "+s.value+" no reconocido");
    }

    /**Metodo al que se llama en el momento en que ya no es posible una recuperación de
    errores.*/
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        /*System.err.println("Error en la Línea " + (s.right+1)+ "Columna "+(s.left+1)+". Identificador " +
        s.value + " no reconocido.");*/
        Inicio.reporteError.agregar("Sintactico",s.right+1,s.left+1,"Identificador "+s.value+" no reconocido");
    
    }

    public Nodo getRaiz(){
        return action_obj.raiz;
    }
:}


action code {:
    Nodo raiz=null;
    
:}

/*terminales reservados*/
terminal String als,var,importar,resEntero,resDecimal,resCaracter,resCadena,resBool,vacio;
terminal String publico,protegido,privado,hereda,nuevo,retornar,llamar,inicio,incluir_HK,llamarHK;
terminal String graphikar_funcion,datos,columna,procesar,donde,dondeCada,dondeTodo,imprimir;

/*Sentencias de control*/
terminal String si,sino,seleccion,caso,defecto,para,mientras,hacer,continuar,terminar;

/*terminales reservados compuestos*/
terminal String idenImportar;


/*terminales de simbolos reservados*/
terminal String puntoComa,parenInicio,parenFin,llaveInicio,llaveFin,inteInicio,inteFin,coma;
terminal String igual,punto,dosPuntos,corInicio,corFin;

/* simbolos de operaciones aritmeticas. */
terminal String mas, menos, mul, div,mod,pot,aumento,disminucion;

/*simbolos de operaciones relacionales*/
terminal String mayor,menor,igualIgual,mayorIgual,menorIgual,noIgual;

/*simbolos de operaciones logicas*/
terminal String or,xor,and,not;

/*
* Este terminal tiene un valor string. Recuerda que le dábamos el valor
* en el código del analizador léxico, al darle como parámetro un valor
* string al objeto Symbolo.
*/
/*Contiene el valor del tipo de dato especifico*/
terminal String decimal,entero,cadena,bool,caracter;
terminal String iden;

/*
* Aquí están los no terminales
*/
non terminal Nodo INICIO,CLASESALS,IMPORTACION,IMPORTACIONES,INCLUIRHASKELS,INCLUIRHASKEL,ALS;
non terminal Nodo ATRIBUTOVAR,ATRIBUTOALS,LISTAID,LISTAID2,CUERPO,CUERPOS,METODO,CUERPOALS;
non terminal Nodo EXPA,EXPL,EXPR,VISIBILIDAD,TIPODATO,TIPODATO2,TIPOMETODO,DATO;
non terminal Nodo LLAMADAMETODO,VALORPARAMETROS,SENTENCIAS,SENTENCIAS1,SENTENCIA;
non terminal Nodo DECLARACION,ASIGNACION,PARAMETRO,PARAMETROS,PARAMETROSSTAR;
non terminal Nodo INDICES,ARREGLO,ARREGLOS,LISTAEXP,ATRIBUTOVARAR,INDICES2;
non terminal Nodo ACCESOVAR,ACCESO,ACCESOAR,IMPRIMIR;
non terminal Nodo SI,SELECCION,PARA,MIENTRAS,HACERMIENTRAS,CONTINUAR,RETORNO,TERMINAR;
non terminal Nodo CASO,CASOS,DEFECTO,SIMPLIFICADA,VARIABLEPARA;
non terminal Nodo GRAFICARFUNCION;

non terminal Nodo LLAMARHK,LLAMARGK;

non terminal Nodo CONSULTASTAR,CONSULTAS,CONSULTA,PROCESAR,FILTRO;
non terminal Nodo DONDE,DONDECADA,DONDETODO,COLUMNA,FILTROS;

non terminal Nodo ENCABEZADO,ENCABEZADOS;

/* Precedences */
precedence left mas, menos;
precedence left mul, div,mod;
precedence left pot;
precedence left aumento,disminucion;
precedence left parenInicio,parenFin;
precedence left igualIgual,mayor,menor,mayorIgual,menorIgual,noIgual;
precedence left or,xor;
precedence left and;
precedence right not;

/* ­­­­­­­­­­­­­­ Declaración de la gramática  ­­­­­­­­­­­ */
start with INICIO; // start with sirve para indicarle al parser con que produccion empezar

INICIO::=ENCABEZADOS:enca CLASESALS:clasesAls {:
        RESULT=new Nodo("archivo",clasesAls.linea-1,clasesAls.columna-1);
        RESULT.add(enca);
        RESULT.add(clasesAls);
        raiz=RESULT;
    :}|CLASESALS:clasesAls {:
        RESULT=new Nodo("archivo",clasesAls.linea-1,clasesAls.columna-1);
        Nodo enca=new Nodo("encabezado",0,0);
        RESULT.add(enca);
        RESULT.add(clasesAls);
        raiz=RESULT;
    :};

ENCABEZADOS::=ENCABEZADOS:lista ENCABEZADO:val 
            {:
                RESULT=lista;
                RESULT.add(val);
            :}
            |ENCABEZADO:val
            {:
                RESULT=new Nodo("encabezado",val.linea-1,val.columna-1);
                RESULT.add(val);
            :};

ENCABEZADO::=IMPORTACION:val {:RESULT=val;:}
            |INCLUIRHASKEL:val {:RESULT=val;:}
            |error inteFin ENCABEZADO:val {:RESULT=val;:};

IMPORTACION::=importar idenImportar:id inteFin {:
                RESULT=new Nodo("importar",id,idright,idleft);
                :};

INCLUIRHASKEL::=incluir_HK iden:id inteFin{:
                RESULT=new Nodo("incluir",id,idright,idleft);
                :};

CLASESALS::=CLASESALS:lista ALS:als {:RESULT=lista;
        RESULT.add(als);
        :}
        |ALS:als {:
        RESULT=new Nodo("clases",als.linea-1,als.columna-1);
        RESULT.add(als);
        :};

ALS::= als iden:id VISIBILIDAD:visi llaveInicio CUERPOALS:cuerpo llaveFin {:
            RESULT=new Nodo("als",id,idright,idright);
            RESULT.add(visi);
            RESULT.add(cuerpo);
            :}
    |als iden:id VISIBILIDAD:visi hereda iden:idHereda llaveInicio CUERPOALS:cuerpo llaveFin {:
            RESULT=new Nodo("als",id,idright,idleft);
            Nodo hereda=new Nodo("hereda",idHereda,idHeredaright,idHeredaleft);
            RESULT.add(hereda);
            RESULT.add(visi);
            RESULT.add(cuerpo);
            :}
    |error als ALS:als {:RESULT=als;:};         


VISIBILIDAD::= dosPuntos publico:cad {:RESULT=new Nodo("visibilidad","publico",cadright,cadleft);:}
            |dosPuntos protegido:cad {:RESULT=new Nodo("visibilidad","protegido",cadright,cadleft);:}
            |dosPuntos privado:cad {:RESULT=new Nodo("visibilidad","privado",cadright,cadleft);:}
            |{:RESULT=new Nodo("visibilidad","publico");:};

TIPODATO::=resEntero:dato {:RESULT=new Nodo("tipo","entero",datoright,datoleft);:}
        |resDecimal:dato {:RESULT=new Nodo("tipo","decimal",datoright,datoleft);:}
        |resCaracter:dato {:RESULT=new Nodo("tipo","caracter",datoright,datoleft);:}
        |resCadena:dato {:RESULT=new Nodo("tipo","cadena",datoright,datoleft);:}
        |resBool:dato {:RESULT=new Nodo("tipo","bool",datoright,datoleft);:};

TIPOMETODO::=resEntero:dato {:RESULT=new Nodo("tipo","entero",datoright,datoleft);:}
        |resDecimal:dato {:RESULT=new Nodo("tipo","decimal",datoright,datoleft);:}
        |resCaracter:dato {:RESULT=new Nodo("tipo","caracter",datoright,datoleft);:}
        |resCadena:dato {:RESULT=new Nodo("tipo","cadena",datoright,datoleft);:}
        |resBool:dato {:RESULT=new Nodo("tipo","bool",datoright,datoleft);:}
        |iden:dato {:RESULT=new Nodo("tipo",dato,datoright,datoleft);:}
        |vacio:dato {:RESULT=new Nodo("tipo","vacio",datoright,datoleft);:};       

//los constructores no llevan parametros
//Declaracion de objetos 
ATRIBUTOALS::=var:var iden:tipo LISTAID:lista inteFin{:
            RESULT=new Nodo("atributoAlsD",varright,varleft);
            Nodo tipoVar=new Nodo("tipo",tipo,tiporight,tipoleft);
            RESULT.add(tipoVar);
            RESULT.add(lista);
            :}
            //Declaracion y asignacion de objetos
            |var:var iden:tipo iden:id VISIBILIDAD:visi igual nuevo iden:tipo2 parenInicio parenFin inteFin{:
            RESULT=new Nodo("atributoAlsDI",varright,varleft);
            Nodo tipoVar=new Nodo("tipo",tipo,tiporight,tipoleft);
            Nodo tipoVar2=new Nodo("tipo",tipo2,tipo2right,tipo2left);
            Nodo objeto=new Nodo("id",id,idright,idleft);
            objeto.add(visi);
            RESULT.add(tipoVar);
            RESULT.add(objeto);
            RESULT.add(tipoVar2);
            :}
            |var:var iden:tipo iden:id VISIBILIDAD:visi igual EXPL:dir inteFin{:
            RESULT=new Nodo("atributoAlsDD",varright,varleft);
            Nodo tipoVar=new Nodo("tipo",tipo,tiporight,tipoleft);
            Nodo objeto=new Nodo("id",id,idright,idleft);
            objeto.add(visi);
            RESULT.add(tipoVar);
            RESULT.add(objeto);
            RESULT.add(dir);
            :}; 

//Declaracion de variables primitivas
ATRIBUTOVAR::=var:var TIPODATO:tipo LISTAID:lista inteFin {:
            RESULT=new Nodo("atributoVarD",varright,varleft);
            RESULT.add(tipo);
            RESULT.add(lista);
            :}
            //Declaracin y asignacion de variables primitivas
            |var:var TIPODATO:tipo iden:id VISIBILIDAD:visi igual EXPL:exp inteFin{:
            RESULT=new Nodo("atributoVarDA",varright,varleft);
            Nodo id1=new Nodo("id",id,idright,idleft);
            id1.add(visi);
            RESULT.add(tipo);
            RESULT.add(id1);
            RESULT.add(exp);
            :};
/*
//atributo arreglo tipo objeto
//atributoAlsArDI=atributo als-arreglo delacado e instanciado
ATRIBUTOALSAR::=var iden:tipo1 iden:id INDICES:indices1 VISIBILIDAD:visi igual nuevo iden:tipo2 INDICES:indices2 inteFin {:
            RESULT=new Nodo("atributoAlsArDI",id,idright,idleft);
            Nodo tipo=new Nodo("tipo",tipo1,tipo1right,tipo1left);
            RESULT.add(tipo);
            RESULT.add(indices1);
            RESULT.add(visi);
            tipo=new Nodo("tipo",tipo2,tipo2right,tipo2left);
            RESULT.add(tipo);
            RESULT.add(indices2);
            :}
            |var iden:tipo iden:id INDICES:indices VISIBILIDAD:visi inteFin {:
            RESULT=new Nodo("atributoAlsArD",id,idright,idleft);
            Nodo tipo1=new Nodo("tipo",tipo,tiporight,tipoleft);
            RESULT.add(tipo1);
            RESULT.add(indices);
            RESULT.add(visi);
            :};
*/

//atributo arreglo tipo primitivo
ATRIBUTOVARAR::=var TIPODATO:tipo iden:id INDICES:indices VISIBILIDAD:visi inteFin{:
            RESULT=new Nodo("atributoVarArD",id,idright,idleft);
            RESULT.add(tipo);
            RESULT.add(indices);
            RESULT.add(visi);
            :}
            |var TIPODATO:tipo iden:id INDICES:indices VISIBILIDAD:visi igual ARREGLO:arreglo inteFin {:
            RESULT=new Nodo("atributoVarArDA",id,idright,idleft);
            RESULT.add(tipo);
            RESULT.add(indices);
            RESULT.add(visi);
            RESULT.add(arreglo);
            :}
            |var TIPODATO:tipo iden:id INDICES:indices VISIBILIDAD:visi igual EXPL:dir inteFin {:
            RESULT=new Nodo("atributoVarArDD",id,idright,idleft);
            RESULT.add(tipo);
            RESULT.add(indices);
            RESULT.add(visi);
            RESULT.add(dir);
            :};

INDICES::=INDICES:lista corInicio EXPL:exp corFin {:RESULT=lista;
        RESULT.add(exp);
        :}
        |corInicio EXPL:exp corFin {:
        RESULT=new Nodo("indices",exp.linea-1,exp.columna-1);
        RESULT.add(exp);
        :};


ARREGLO::=llaveInicio LISTAEXP:lista llaveFin {:RESULT=lista;:}
        |llaveInicio ARREGLOS:lista llaveFin {:RESULT=lista;:};

ARREGLOS::=ARREGLOS:lista coma ARREGLO:ar {:RESULT=lista;
        for(Nodo e:ar.hijos){
            RESULT.add(e);
        }
        :}
        |ARREGLO:ar {:
        RESULT=ar;    
        :};

LISTAEXP::=LISTAEXP:lista coma EXPL:exp {:RESULT=lista;
        RESULT.add(exp);
        :}
        |EXPL:exp {:
        RESULT=new Nodo("listaExp",exp.linea-1,exp.columna-1);
        RESULT.add(exp);
        :};

//listado de ids para variables globales(la difencia entre LISTAID2 es que en LISTAID puede o no venir visibilidad)
LISTAID::=LISTAID:lista coma iden:id VISIBILIDAD:visi {:RESULT=lista;
            Nodo id1=new Nodo("id",id,idright,idleft);
            id1.add(visi);
            RESULT.add(id1);
            :}
        |iden:id VISIBILIDAD:visi {:
            RESULT=new Nodo("listaIds",idright,idleft);
            Nodo id1=new Nodo("id",id,idright,idleft);
            id1.add(visi);
            RESULT.add(id1);
            :};

CUERPOS::=CUERPOS:cuerpos CUERPO:cuerpo {:RESULT=cuerpos;
            RESULT.add(cuerpo);
            :}
        |CUERPO:cuerpo {:
            RESULT=new Nodo("cuerpo",cuerpo.linea-1,cuerpo.columna-1);
            RESULT.add(cuerpo);
            :};

CUERPO::=ATRIBUTOVAR:atributo {:RESULT=atributo;:} 
        |METODO:metodo {:RESULT=metodo;:}
        |ATRIBUTOALS:atributoAls {:RESULT=atributoAls;:}
        |ATRIBUTOVARAR:arregloVar {:RESULT=arregloVar;:}
        |error inteFin CUERPO:cuerpo {:RESULT=cuerpo;:};

CUERPOALS::=CUERPOS:cuerpos {:RESULT=cuerpos;:}
    |{:RESULT=new Nodo("cuerpo",0,0);:};

//expresiones artmeticas---falta llamada metodo,aumento y decremento
EXPA::=EXPA:d1 mas:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 menos:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 mul:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 div:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 pot:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 mod:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |parenInicio EXPL:d1 parenFin {:RESULT=d1;:}
    |menos EXPA:exp {:RESULT=new Nodo("unitario",exp.linea-1,exp.columna-1);RESULT.add(exp);:}
    |DATO:d1 {:RESULT=d1;:}
   // |iden:id1 {:RESULT=new Nodo("id",id1,id1right,id1left);:}
    |ACCESO:id1 {:RESULT=id1;:}
    |LLAMARHK:id1 {:RESULT=id1;:}
    |LLAMARGK:id1 {:RESULT=id1;:}
    |SIMPLIFICADA:val {:RESULT=val;:}
    |COLUMNA:val {:RESULT=val;:};

EXPR::=EXPA:d1 igualIgual:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 mayor:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 menor:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 mayorIgual:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 menorIgual:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 noIgual:op EXPA:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPA:d1 {:RESULT=d1;:};

EXPL::=EXPL:d1 and:op EXPL:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPL:d1 or:op EXPL:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |EXPL:d1 xor:op EXPL:d2 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);RESULT.add(d2);:}
    |not:op EXPL:d1 {:RESULT=new Nodo(op,opright,opleft);RESULT.add(d1);:}
    |EXPR:d1 {:RESULT=d1;:};

DATO::=entero:dato {:RESULT=new Nodo("entero",dato,datoright,datoleft);:}
    |decimal:dato {:RESULT=new Nodo("decimal",dato,datoright,datoleft);:}
    |caracter:dato {:dato=dato.replace("'","");RESULT=new Nodo("caracter",dato,datoright,datoleft);:}
    |cadena:dato {:dato=dato.replace("\"","");RESULT=new Nodo("cadena",dato,datoright,datoleft);:}
    |bool:dato {:RESULT=new Nodo("bool",dato,datoright,datoleft);:};


//faltan parametros
METODO::=TIPOMETODO:tipo iden:id parenInicio PARAMETROSSTAR:para parenFin VISIBILIDAD:visi llaveInicio SENTENCIAS:sent llaveFin{:
        RESULT=new Nodo("metodo",id,idright,idleft);
        RESULT.add(tipo);
        RESULT.add(visi);
        RESULT.add(para);
        RESULT.add(sent);
        :}
        |vacio inicio:va parenInicio parenFin llaveInicio SENTENCIAS:sent llaveFin {:
        RESULT=new Nodo("inicio","inicio",varight,valeft);
        RESULT.add(sent);
        :}
        |vacio datos:va parenInicio parenFin llaveInicio CONSULTASTAR:sent llaveFin {:
        RESULT=new Nodo("datos","datos",varight,valeft);
        RESULT.add(sent);
        :};


PARAMETROSSTAR::=PARAMETROS:lista {:RESULT=lista;:}
                |{:RESULT=new Nodo("parametros",0,0);:};

PARAMETROS::=PARAMETROS:lista coma PARAMETRO:par {:RESULT=lista;
            RESULT.add(par);
            :}
            |PARAMETRO:par {:
            RESULT=new Nodo("parametros",par.linea-1,par.columna-1);
            RESULT.add(par);
            :};

TIPODATO2::=TIPODATO:tipo {:RESULT=tipo;:}
        |iden:dato {:RESULT=new Nodo("tipo",dato,datoright,datoleft);:};

PARAMETRO::=TIPODATO2:tipo iden:id {:
    RESULT=new Nodo("parametro",id,idright,idleft);
    RESULT.add(tipo);
    :}
    |TIPODATO2:tipo iden:id INDICES2:dim {:
    RESULT=new Nodo("parametroAr",id,idright,idleft);
    RESULT.add(tipo);
    RESULT.add(dim);
    :};

INDICES2::=INDICES2:lista corInicio corFin {:RESULT=lista;
        Nodo n=new Nodo("dimension",0,0);
        RESULT.add(n);
        :}
        |corInicio:exp corFin {:
        RESULT=new Nodo("indices",expright,expleft);
        Nodo n=new Nodo("dimension",0,0);
        RESULT.add(n);
        :};

VALORPARAMETROS::=VALORPARAMETROS:lista coma EXPL:exp{:RESULT=lista;
                RESULT.add(exp);
                :}
                |EXPL:exp {:
                RESULT=new Nodo("valorParametros",expright,expleft);
                RESULT.add(exp);
                :};

//produccion de sentencias
SENTENCIAS1::=SENTENCIAS1:lista SENTENCIA:sent {:RESULT=lista;
            RESULT.add(sent);
            :}
            |SENTENCIA:sent {:
            RESULT=new Nodo("sentencias",sent.linea-1,sent.columna-1);
            RESULT.add(sent);
            :};

SENTENCIAS::=SENTENCIAS1:sent {:RESULT=sent;:}
            |{:RESULT=new Nodo("sentencias",0,0);:};

//----------------------------------------------------------------
//----------------------------------------------------------------
//----------------------------------------------------------------



SENTENCIA::=LLAMARHK:sent inteFin {:RESULT=sent;:}
        |LLAMARGK:sent inteFin {:RESULT=sent;:}
        |DECLARACION:sent inteFin {:RESULT=sent;:}
        |ASIGNACION:sent inteFin {:RESULT=sent;:}
        |SIMPLIFICADA:sent inteFin {:RESULT=sent;:}
        |SI:sent {:RESULT=sent;:}
        |SELECCION:sent {:RESULT=sent;:}
        |PARA:sent {:RESULT=sent;:}
        |MIENTRAS:sent {:RESULT=sent;:}
        |HACERMIENTRAS:sent inteFin {:RESULT=sent;:}
        |RETORNO:sent inteFin {:RESULT=sent;:}
        |TERMINAR:sent inteFin {:RESULT=sent;:}
        |CONTINUAR:sent inteFin {:RESULT=sent;:}
        |GRAFICARFUNCION:sent inteFin {:RESULT=sent;:}
        |IMPRIMIR:sent inteFin {:RESULT=sent;:}
        |error inteFin SENTENCIA:sent {:RESULT=sent;:};
//----------------------------------------------------------------
//----------------------------------------------------------------
//----------------------------------------------------------------

LISTAID2::=LISTAID2:lista coma iden:id {:RESULT=lista;
            Nodo id1=new Nodo("id",id,idright,idleft);
            RESULT.add(id1);
            :}
        |iden:id {:
            RESULT=new Nodo("listaIds",idright,idleft);
            Nodo id1=new Nodo("id",id,idright,idleft);
            RESULT.add(id1);
            :};
//varLocalD=variable local declarada
//VarLocalDA=variable local declarada y asignada
DECLARACION::=var:var TIPODATO:tipo LISTAID2:lista {:
            RESULT=new Nodo("varLocalD",varright,varleft);
            RESULT.add(tipo);
            RESULT.add(lista);
            :}
            |var:var TIPODATO:tipo iden:id igual EXPL:exp 
            {:
            RESULT=new Nodo("varLocalDA",varright,varleft);
            Nodo id1=new Nodo("id",id,idright,idleft);
            RESULT.add(tipo);
            RESULT.add(id1);
            RESULT.add(exp);
            :}
            |var:var iden:tipo LISTAID2:lista {:
            RESULT=new Nodo("varLocalAlsD",varright,varleft);
            Nodo tipoVar=new Nodo("tipo",tipo,tiporight,tipoleft);
            RESULT.add(tipoVar);
            RESULT.add(lista);
            :}
            |var:var iden:tipo1 iden:id igual nuevo iden:tipo2 parenInicio parenFin  {:
            RESULT=new Nodo("varLocalAlsDI",varright,varleft);
            Nodo n1=new Nodo("id",id,idright,idleft);
            Nodo tipoVar=new Nodo("tipo",tipo1,tipo1right,tipo1left);
            RESULT.add(tipoVar);
            RESULT.add(n1);
            tipoVar=new Nodo("tipo",tipo2,tipo2right,tipo2left);
            RESULT.add(tipoVar);
            :}
            |var:var iden:tipo1 iden:id igual EXPL:dir  {:
            RESULT=new Nodo("varLocalAlsDD",varright,varleft);
            Nodo n1=new Nodo("id",id,idright,idleft);
            Nodo tipoVar=new Nodo("tipo",tipo1,tipo1right,tipo1left);
            RESULT.add(tipoVar);
            RESULT.add(n1);
            RESULT.add(dir);
            :}
            |var TIPODATO:tipo iden:id INDICES:indices {:
            RESULT=new Nodo("varLocalArD",id,idright,idleft);
            RESULT.add(tipo);
            RESULT.add(indices);
            :}
            |var TIPODATO:tipo iden:id INDICES:indices igual ARREGLO:arreglo {:
            RESULT=new Nodo("varLocalArDA",id,idright,idleft);
            RESULT.add(tipo);
            RESULT.add(indices);
            RESULT.add(arreglo);
            :}
            |var TIPODATO:tipo iden:id INDICES:indices igual EXPL:dir {:
            RESULT=new Nodo("varLocalArDD",id,idright,idleft);
            RESULT.add(tipo);
            RESULT.add(indices);
            RESULT.add(dir);
            :};

ASIGNACION::= ACCESO:acc igual EXPL:exp {:
    RESULT=new Nodo("asignacion",acc.linea-1,acc.columna-1);
    RESULT.add(acc);
    RESULT.add(exp);
    :}
    |ACCESO:acc igual ARREGLO:ar {:
    RESULT=new Nodo("asignacionAr",acc.linea-1,acc.columna-1);
    RESULT.add(acc);
    RESULT.add(ar);
    :}
    //intanciacion de un previamente declarado
    |ACCESO:acc igual nuevo iden:tipo2 parenInicio parenFin {:
    RESULT=new Nodo("asignacionAlsI",acc.linea-1,acc.columna-1);
    RESULT.add(acc);
    Nodo tipoVar=new Nodo("tipo",tipo2,tipo2right,tipo2left);
    RESULT.add(tipoVar);
    :};




//----------------------------------------------------------------
//sentencias de control
SI::= si:si parenInicio EXPL:exp parenFin llaveInicio SENTENCIAS:sent1 llaveFin {:
    RESULT=new Nodo("si",siright,sileft);
    RESULT.add(exp);
    RESULT.add(sent1);
    Nodo sent2=new Nodo("sentencias",0,0);
    RESULT.add(sent2);
    :}
    |si:si parenInicio EXPL:exp parenFin llaveInicio SENTENCIAS:sent1 llaveFin sino llaveInicio SENTENCIAS:sent2 llaveFin {:
    RESULT=new Nodo("si",siright,sileft);
    RESULT.add(exp);
    RESULT.add(sent1);
    RESULT.add(sent2);
    :};  


SELECCION::=seleccion:sel parenInicio ACCESO:acc parenFin llaveInicio CASOS:lista llaveFin {:
    RESULT=new Nodo("seleccion",selright,selleft);
    RESULT.add(acc);
    Nodo defecto=new Nodo("defecto",0,0);
    Nodo sent2=new Nodo("sentencias",0,0);
    defecto.add(sent2);
    lista.add(defecto);
    RESULT.add(lista);
    :}
    |seleccion:sel parenInicio ACCESO:acc parenFin llaveInicio CASOS:lista DEFECTO:def llaveFin{:
    RESULT=new Nodo("seleccion",selright,selleft);
    RESULT.add(acc);
    lista.add(def);
    RESULT.add(lista);
    :};

CASOS::=CASOS:lista CASO:caso {:RESULT=lista;
    RESULT.add(caso);
    :}
    |CASO:caso {:
    RESULT=new Nodo("casos",caso.linea-1,caso.columna-1);
    RESULT.add(caso);
    :};

CASO::=caso:ca EXPL:exp dosPuntos SENTENCIAS:sent{:
    RESULT=new Nodo("caso",caright,caleft);
    RESULT.add(exp);
    RESULT.add(sent);
    :};

DEFECTO::=defecto:def dosPuntos SENTENCIAS:sent {:
    RESULT=new Nodo("defecto",defright,defleft);
    RESULT.add(sent);
    :};

SIMPLIFICADA::= EXPA:exp aumento {:RESULT=new Nodo("++",exp.linea-1,exp.columna-1);
            RESULT.add(exp);
            :}
            |EXPA:exp disminucion {:RESULT=new Nodo("--",exp.linea-1,exp.columna-1);
            RESULT.add(exp);
            :};

PARA::=para:para parenInicio VARIABLEPARA:var dosPuntos EXPL:exp dosPuntos SIMPLIFICADA:op parenFin llaveInicio SENTENCIAS:sent llaveFin {:
    RESULT=new Nodo("para",pararight,paraleft);
    RESULT.add(var);
    RESULT.add(exp);
    RESULT.add(op);
    RESULT.add(sent);
    :};

VARIABLEPARA::=var:var resEntero:tipo iden:id igual EXPL:exp {:
            RESULT=new Nodo("varLocalDA",varright,varleft);
            Nodo id1=new Nodo("id",id,idright,idleft);
            Nodo t=new Nodo("tipo",tipo,tiporight,tipoleft);
            RESULT.add(t);
            RESULT.add(id1);
            RESULT.add(exp);
            :}
            | ACCESO:acc igual EXPL:exp {:
            RESULT=new Nodo("asignacion",acc.linea-1,acc.columna-1);
            RESULT.add(acc);
            RESULT.add(exp);
            :};

MIENTRAS::=mientras:mi parenInicio EXPL:exp parenFin llaveInicio SENTENCIAS:sent llaveFin {:
    RESULT=new Nodo("mientras",miright,mileft);
    RESULT.add(exp);
    RESULT.add(sent);
    :};

HACERMIENTRAS::=hacer:hacer llaveInicio SENTENCIAS:sent llaveFin mientras parenInicio EXPL:exp parenFin {:
    RESULT=new Nodo("hacerMientras",hacerright,hacerleft);
    RESULT.add(exp);
    RESULT.add(sent);
    :};

RETORNO::=retornar:ret {:
        RESULT=new Nodo("retorno",retright,retleft);
        :}
        |retornar:ret EXPL:exp {:
        RESULT=new Nodo("retorno",retright,retleft);
        RESULT.add(exp);    
        :};

CONTINUAR::=continuar:con {:RESULT=new Nodo("continuar",conright,conleft);:};

TERMINAR::=terminar:ter {:RESULT=new Nodo("terminar",terright,terleft);:};

GRAFICARFUNCION::=graphikar_funcion:g parenInicio ACCESO:acc1 coma ACCESO:acc2 parenFin {:
    RESULT=new Nodo("graficarFuncion",gright,gleft);
    RESULT.add(acc1);
    RESULT.add(acc2);
    :}; 

IMPRIMIR::=imprimir:imp parenInicio EXPL:exp parenFin {:
    RESULT=new Nodo("imprimir",impright,impleft);
    RESULT.add(exp);
    :};

//--------------------------------------------------------------
//----------acceso a los atributos y metodos de objetos---------
//llamada a funciones haskell++
LLAMARHK::=llamarHK iden:id parenInicio VALORPARAMETROS:lista parenFin {:
            RESULT=new Nodo("llamadaMetodoHK",id,idright,idleft);
            RESULT.add(lista);
            :}
            |llamarHK iden:id parenInicio parenFin {:
            RESULT=new Nodo("llamadaMetodoHK",id,idright,idleft);
            Nodo n1=new Nodo("valorParametros",0,0);
            RESULT.add(n1);
            :};

LLAMARGK::=llamar ACCESO:lista punto LLAMADAMETODO:val 
        {:
            RESULT=lista;
            lista.hijos.add(val);
        :}
        |llamar  LLAMADAMETODO:val
        {:
            RESULT=new Nodo("acceso",val.linea-1,val.columna-1);  
            RESULT.add(val);
        :}; 


ACCESO::=ACCESO:lista punto ACCESOVAR:acc {:RESULT=lista;
        RESULT.add(acc);
        :}
        |ACCESOVAR:acc {:
        RESULT=new Nodo("acceso",acc.linea-1,acc.columna-1);
        RESULT.add(acc);
        :};

//llamada metodo
LLAMADAMETODO::=iden:id parenInicio VALORPARAMETROS:lista parenFin {:
            RESULT=new Nodo("llamadaMetodo",id,idright,idleft);
            RESULT.add(lista);
            :}
            |iden:id parenInicio parenFin {:
            RESULT=new Nodo("llamadaMetodo",id,idright,idleft);
            Nodo n1=new Nodo("valorParametros",0,0);
            RESULT.add(n1);
            :}
            |datos:id parenInicio parenFin {:
            RESULT=new Nodo("llamadaMetodo",id,idright,idleft);
            Nodo n1=new Nodo("valorParametros",0,0);
            RESULT.add(n1);
            :};


ACCESOVAR::=LLAMADAMETODO:lla {:RESULT=lla;:}
            |iden:id {:RESULT=new Nodo("id",id,idright,idleft);:}
            |ACCESOAR:lla {:RESULT=lla;:};

//acceso a arreglos
ACCESOAR::=iden:id INDICES:lista {:
    RESULT=new Nodo("accesoAr",id,idright,idleft);
    RESULT.add(lista);
    :};


//-------------------------fin-----------------------------------



//------------------------SENTENCIAS DE FUNCION DATOS------------------
COLUMNA::= columna:c parenInicio EXPL:exp parenFin {:
    RESULT=new Nodo("columna",cright,cleft);
    RESULT.add(exp);
    :};


CONSULTASTAR::=CONSULTAS:lista {:
            RESULT=lista;
            :}
            |{:RESULT=new Nodo("consultas",0,0);:};

CONSULTAS::=CONSULTAS:lista CONSULTA:val 
        {:
            RESULT=lista;
            for(int i=0;i<val.hijos.size();i++){
                lista.add(val.hijos.get(i));
            }
        :}
        |CONSULTA:val 
        {:
            RESULT=new Nodo("consultas",val.linea-1,val.columna-1);
            RESULT.hijos=val.hijos;
        :};

CONSULTA::=PROCESAR:val FILTROS:fil 
        {:
            RESULT=new Nodo("consulta",val.linea-1,val.columna-1);
            RESULT.add(val);
            for(int i=0;i<fil.hijos.size();i++){
                RESULT.add(fil.hijos.get(i));
            }
        :}
        |PROCESAR:val 
        {:
            RESULT=new Nodo("consulta",val.linea-1,val.columna-1);
            RESULT.add(val);
        :}
        |error inteFin CONSULTA:con {:RESULT=con;:};

PROCESAR::=procesar:p igual EXPL:exp inteFin
        {:
            RESULT=new Nodo("procesar",pright,pleft);
            RESULT.add(exp);
        :};

FILTROS::=FILTROS:lista FILTRO:f 
        {:
            RESULT=lista;
            RESULT.add(f);
        :}
        |FILTRO:f
        {:
            RESULT=new Nodo("filtros",fright,fleft);
            RESULT.add(f);
        :};

FILTRO::=DONDE:val inteFin 
    {:
        RESULT=val;
    :}
    |DONDECADA:val inteFin 
    {:
        RESULT=val;
    :}
    |DONDETODO:val inteFin 
    {:
        RESULT=val;
    :};

DONDE::=donde:d parenInicio EXPL:exp1 parenFin igual EXPL:exp2
        {:
            RESULT=new Nodo("donde",dright,dleft);
            RESULT.add(exp1);
            RESULT.add(exp2);
        :};

DONDECADA::=dondeCada:d parenInicio EXPL:exp parenFin
        {:
            RESULT=new Nodo("dondeCada",dright,dleft);
            RESULT.add(exp);
        :};

DONDETODO::=dondeTodo:d parenInicio EXPL:exp parenFin 
        {:
            RESULT=new Nodo("dondeTodo",dright,dleft);
            RESULT.add(exp);
        :};
